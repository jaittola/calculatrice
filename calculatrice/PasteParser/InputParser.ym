%{

#if defined(PARSER_UNITTEST)

#import "parser_unittest.h"
#else
#import "ParserAdapter.h"
#endif

#import "parsed_types.h"

int yylex(void);
void yyerror(char *);

%}

%union {
    int sign;
    expression_t* expression;
};

%token <expression> UNSIGNED_INTEGER
%token <expression> UNSIGNED_DOUBLE
%token <sign> MINUS PLUS
%token IMAG DASH ANGLE DEGREE BRACKET_L BRACKET_R NEWLINE DOUBLESPACE

%type <expression> expr
%type <expression> scalar_value integer_value double_value imaginary_or_real_value
%type <expression> fraction_value true_fraction_value mixed_fraction_value
%type <expression> complex_value cartesian_complex_value cartesian_polar_value
%type <expression> matrix_row_elements matrix_rows matrix_value
%type <sign> sign maybesign

%%

program: expr {
    if (parserAdapter != nil) {
        [parserAdapter setParseResult:$1];
    }
#if !defined(PARSER_UNITTEST)
    else {
        NSLog(@"Parser adapter not defined");
    }

    expression_free($1);
#endif
}
;

expr: imaginary_or_real_value | matrix_value;

imaginary_or_real_value: scalar_value | complex_value;

complex_value: cartesian_complex_value | cartesian_polar_value;

scalar_value: integer_value | double_value | fraction_value;

integer_value: maybesign UNSIGNED_INTEGER {
    $$ = expression_neg($1, $2);
}
;

double_value: maybesign UNSIGNED_DOUBLE {
    $$ = expression_neg($1, $2);
}
;

fraction_value: true_fraction_value | mixed_fraction_value;

true_fraction_value: maybesign UNSIGNED_INTEGER DASH UNSIGNED_INTEGER {
    $$ = expression_fraction(expression_neg($1, $2), $4, NULL);
}
;

mixed_fraction_value: maybesign UNSIGNED_INTEGER UNSIGNED_INTEGER DASH UNSIGNED_INTEGER {
    $$ = expression_fraction(expression_neg($1, $2), $3, $5);
}
;

cartesian_complex_value:
scalar_value sign scalar_value IMAG {
    $$ = expression_complex_cart($1, expression_neg($2, $3));
}
| scalar_value sign IMAG {
    $$ = expression_complex_cart($1, expression_one($2));
}
| scalar_value IMAG {
    $$ = expression_complex_cart(expression_zero(), $1);
}
| maybesign IMAG {
    $$ = expression_complex_cart(expression_zero(), expression_one($1));
}
;

cartesian_polar_value:
scalar_value ANGLE scalar_value DEGREE {
    $$ = expression_complex_polar($1, $3, e_au_deg);
}
| scalar_value ANGLE scalar_value {
    $$ = expression_complex_polar($1, $3, e_au_rad);
}
;

matrix_value:
BRACKET_L matrix_rows BRACKET_R {
    $$ = $2;
};

matrix_rows: matrix_rows NEWLINE matrix_row_elements {
    $$ = expression_append_sibling($1, $3);
}
| matrix_row_elements {
    expression_t *expressions[] = {$1, NULL};
    $$ = expression_matrix(expressions);
}
;

matrix_row_elements: matrix_row_elements DOUBLESPACE imaginary_or_real_value {
    $$ = expression_append_sibling($1, $3);
}
| imaginary_or_real_value {
    expression_t *expressions[] = {$1, NULL};
    $$ = expression_matrix_row(expressions);
}
;

sign: PLUS | MINUS;

maybesign:
sign { $$ = $1; }
| { $$ = 1; }
;

%%

void yyerror(char *s)
{
    if (parserAdapter != nil) {
        [parserAdapter setParseError:s];
    }
#if !defined(PARSER_UNITTEST)
    else {
        NSLog(@"Parsing failed: %s", s);
    }
#endif
}
